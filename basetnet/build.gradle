apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

def VERSION = '1.0.2'

// https://bintray.com/zhaoyubetter
//  上传命令：gradlew clean build generatePomFileForMavenPublication bintrayUpload -PbintrayUser=zhaoyubetter -PbintrayKey= -PdryRun=false
android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName VERSION
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

//添加
publish {
    userOrg = 'zhaoyubetter'//bintray.com用户名
    groupId = 'com.github.lib'//jcenter上的路径
    artifactId = 'basenet'//项目名称
    publishVersion = VERSION //版本号
    desc = '使用okhttp封装了基本的网络请求'//描述，不重要
    website = 'https://github.com/zhaoyubetter/basenet'//网站，不重要
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
}
